// Title: Power for P Test Statistic with Endogeneity and Long Memory Regressor 
// Author: Sepideh Mosaferi
// Date: April 2023

new;
library pgraph;
pqgwin many;

// test statistic
proc ( 1 ) = Ptest(y,x);
    local n,xmat,theta,gxtheta,u_hat,num_rho,denom_rho,s,rho_hat,v_hat,t,M,k,a_hat_k,u,Uhat_M;
    n = rows(y);       
    xmat = zeros(n, 2);
    xmat[.,1]=ones(n,1);
    xmat[.,2]=x;
    theta=pinv(xmat'*xmat)*xmat'*y;
    gxtheta = theta[1]+theta[2]*x; 
    u_hat = (y-gxtheta);
    num_rho = zeros(n-1,1);
    denom_rho = zeros(n-1,1);
    s = 2;
    do until s > (n-1);
        num_rho[s] = u_hat[s]*u_hat[s-1];
        denom_rho[s] = u_hat[s-1]^2;
        s = s+1;
    endo;
    rho_hat = sumc(num_rho)/sumc(denom_rho);
    v_hat = zeros(n,1);
    v_hat[1] = u_hat[1];
    t = 2;
    do until t > n;
        v_hat[t] = u_hat[t]-rho_hat*u_hat[t-1];
        t = t+1;
    endo;
    //M={6,12,18}
    M=18;
    a_hat_k = zeros(M,1);
    k=1;
    do until k > M;
        a_hat_k[k] = sumc(v_hat[(k+1):n].*v_hat[1:(n-k)])/sumc(v_hat^2);
        k = k+1;
    endo;
    Uhat_M = zeros(M,1);
    u=1;
    do until u > M;
    Uhat_M[u] = n*(n+2)*(a_hat_k[u]^2/(n-k)); 
        u = u+1;
    endo;
    Uhat_M = sumc(Uhat_M); 
    retp(Uhat_M);
endp;  
    

// proc to generate long memory data by looping 
proc dsim(u,d);
/*
** simulate I(d) data
** input  = u
** output = (I-L)^-d u
*/
local nn,y,b0,k,b,ur;
nn= rows(u);
b = zeros(nn,1);
y = zeros(nn,1);
b0=1;          
k =1;
do until k>nn;
   if k==1;
      b[k]=d;
   else;   
      b[k]=(k+d-1)*b[k-1]/k;
   endif;
   k = k+1;
endo;  
b=b0|b;
k =1;
do until k>nn;
   ur   = rev(u[1:k]);   
   y[k] = b[1:k]'ur;
   k = k+1;
endo;   
retp(y);
endp;


/* main program   */
 library gauss, pgraph;
 pqgwin many;
   
   seed1  = 78912342;
   iter   = 2000;             @ # iterations  @
   numh   = {100, 500, 1000, 5000}; @ sample size selections @
          
d01 = 0.1;   @ value of d @
d02 = 0.2;
d03 = 0.3; 
d04 = 0.4;

aa     = 0.25; 	       @ AR coefficient for equation error AR model @
rho    = 1;            @ correlation between ux and uy for endogeneity @ 
b  = 1;                @ regression coefficient @	
sig = 0.2;             @ error standard deviation @

// sample size selection  
nn = numh[2]; 
  
alpha = {0.01, 0.05, 0.10, 0.15, 0.20, 0.25, 0.30, 0.35, 0.40, 0.45, 0.50};  @ alpha=nominal level type I error @

// saving results
Ind_accept = zeros(iter,rows(alpha));
  
  k = 1;
  do until k > iter;            @ loop for simulation @
      
      ux  = rndns(nn,1,seed1);    // regressor error 
      uy  = rndns(nn,1,seed1);           // equation error
      ux  = ((1-rho^2)^0.5) *ux + rho*uy;  // endogeneity correlation = rho 
      uy  = recserar(uy,uy[1],aa);
      
      uxd01 = dsim(ux,d01);  // generate long memory inputs for regressor [case: d01]. This could be changed for the other values of d.
      xd01 = recserar(uxd01,uxd01[1],1); 
      
      hpower  = 1/3; 
      h_N = nn^(-hpower);
      nu = 3;
      fxd01_HA = xd01+(abs(xd01)^nu)*(1/(nn^(0.25+(nu/3))*(h_N^0.25)));            
      yd01_HA = b*fxd01_HA + sig*uy;   // cointegrated regression equation [case: d01]. This could be changed for the other values of d.
          
      // test statistic
      Test = Ptest(yd01_HA,xd01);
      
      // loop result
      //M={6,12,18}
      M=18;
      pvalue = cdfChic(Test, M-1);
      
      // indicator of acceptance    
      z=1;
      do until z > rows(alpha);  
          Ind_accept[k,z] = pvalue <= alpha[z];  @ acceptance indicator @
          z = z+1;
      endo;

      k = k+1;
  endo;
  
  // results
  Ind_accept = packr(Ind_accept);
  
  // power of test
  Power = meanc(Ind_accept);
  
  // writing the results
  Result = alpha~Power;
  
/*
Save the results
*/
   
Output file = "/Users/sepidehmosaferi/Desktop/Result.txt" ON; /* this opens the text file to store the results */
Result;
Output off; /* this closes the text file to store the results */    

